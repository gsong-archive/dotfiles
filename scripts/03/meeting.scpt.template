property utils : load script POSIX file "@directory/utils.scpt"

# Main functions
on startMeeting()
  unloadCrashPlan()
  setMessagesMeeting()
  setSkypeDND()
  pauseItunes()
  pauseRdio()
end startMeeting

on endMeeting()
  loadCrashPlan()
  setMessagesWorking()
  setSkypeOnline()
end endMeeting

# Load and unload CrashPlan
on unloadCrashPlan()
  try
    run script POSIX file "@directory/unload_crashplan.scpt"
  end try
end unloadCrashPlan

on loadCrashPlan()
  try
    run script POSIX file "@directory/crashplan.scpt"
  end try
end loadCrashPlan

# Change Messages status
on setMessagesWorking()
  tell utils
    if appIsRunning("Messages") then
      tell application "Messages"
        set status to available
        set status message to "Working"
      end tell
    end if
  end tell
end setMessagesWorking

on setMessagesMeeting()
  tell utils
    if appIsRunning("Messages") then
      tell application "Messages"
        set status to away
        set status message to "In a meeting"
      end tell
    end if
  end tell
end setMessagesMeeting

# Change Skype status
on setSkypeDND()
  setSkypeStatus("Do not disturb")
end setSkypeDND

on setSkypeOnline()
  setSkypeStatus("Online")
end setSkypeOnline

on setSkypeStatus(status)
  tell utils
    if appIsRunning("Skype") then
      set currentApp to GetCurrentApp()
      activate application "Skype"
      try
        tell application "System Events"
          click menu item ¬
            status in menu "Change status" in menu item ¬
            "Change Status" in menu "File" of menu bar item ¬
            "File" in menu bar 1 of process "Skype"
        end tell
      end try
      activate application currentApp
    end if
  end tell
end setSkypeStatus

# Pause iTunes
on pauseItunes()
  tell utils
    if appIsRunning("iTunes") then
      tell application "iTunes" to pause
    end if
  end tell
end pauseItunes

# Pause Rdio
on pauseRdio()
  tell utils
    if appIsRunning("Rdio") then
      tell application "Rdio" to pause
    end if
  end tell
end pauseRdio
